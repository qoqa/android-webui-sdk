plugins {
    id("com.android.library")
    id 'maven-publish'
}

android {
    namespace "com.queue_it.androidsdk"
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "2.0.13"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation 'com.squareup.okhttp3:okhttp:3.8.1'
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'com.queue_it.androidsdk'

    publishedGroupId = 'com.queue_it.androidsdk'
    libraryName = 'com.queue_it.androidsdk'
    artifact = 'library'

    libraryDescription = 'Android SDK to integrate with Queue-it'

    siteUrl = 'https://github.com/queueit/android-sdk'
    gitUrl = 'https://github.com/queueit/android-sdk.git'

    libraryVersion = android.defaultConfig.versionName

    licenseName = 'MIT'
    licenseUrl = 'MIT'
    allLicenses = ["MIT"]
}


group = publishedGroupId                               // Maven Group ID for the artifact

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = artifact
            version = libraryVersion

            afterEvaluate {
                from components.release
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'

version = libraryVersion

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        userOrg = 'queueit'
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = true
            }
        }
    }
}